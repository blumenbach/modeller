group 'org.blume'
version '0.1-SNAPSHOT'

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: "application"

repositories {
    maven { url "http://spring-rich-c.sourceforge.net/maven2repository" }
    mavenCentral()
    flatDir {
        dirs "$rootProject.projectDir/lib"
    }
}

mainClassName = "org.blume.modeller.ModellerApplication"

description = """Spring richclient Modeller GUI"""

jar.baseName="modeller"

dependencies {
    compile project(':modeller-core'),
            project(':modeller-business'),
            project(':modeller-fedora'),
            project(':modeller-djatoka'),
            project(':modeller-hocr'),
            'com.vlsolutions:vldocking:2.1.4',
            'net.sf.nachocalendar:nachocalendar:0.23',
            'org.springframework.richclient:spring-richclient-resources:1.1.0',
            'org.springframework.richclient:spring-richclient-docking:1.0.0',
            'org.springframework.richclient:spring-richclient-sandbox:1.0.0',
            'org.springframework.richclient:spring-richclient-core:1.1.0',
            'org.springframework.richclient:spring-richclient-jdk6:1.1.0',
            'org.springframework.richclient:spring-richclient-vldocking:1.1.0' //cause the maven repo doesn't have this version
    compile group: 'com.jgoodies', name: 'forms', version: '1.2.1'
    compile group: 'org.swinglabs', name: 'swingx', version: '1.6.1'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.7'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version: '1.4.0.RELEASE'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    runtime 'com.jgoodies:looks:2.0.4'
}

//include the docs and licenses into the distributable zip/tar
distributions {
    main{
        contents {
            //include the various user guides
            from("doc") {
                into "doc"
            }

            //include the keystore used for signing jars
            from("${rootDir.path}/keystore") {
                into "keystore"
            }

            //include helpful scripts
            from("scripts") {
                into "bin"
            }
        }
    }
}

startScripts {
    if(project.version == "unspecified"){
        applicationDefaultJvmArgs += ["-Xdebug", "-Xrunjdwp:server=y,transport=dt_socket,address=4000,suspend=n"]
    }

    unixStartScriptGenerator.template = resources.text.fromFile(project.projectDir.path + '/customUnixStartScript.txt')
    windowsStartScriptGenerator.template = resources.text.fromFile(project.projectDir.path + '/customWindowsStartScript.txt')
}

afterEvaluate {
    configure(tasks.processResources) {
        eachFile { copyDetails ->
            if (copyDetails.name == 'about.txt' || copyDetails.name == 'messages.properties') {
                filter(ReplaceTokens, tokens:[VERSION: project.version])
            }
        }
    }
}